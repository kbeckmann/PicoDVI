;
; Copyright (c) 2022-2023 Konrad Beckmann
;
; SPDX-License-Identifier: BSD-3-Clause
;

.program n64

; Video
; Data is stable on Negedge of CLK
; 0-6 = data
; 7   = dsync
; 8   = clk

; Audio (pinout matches the drawing in the repo)
;  9 = LRCLK
; 10 = SDAT
; 11 = BCLK

; Joybus RX
; 20 = P1 Data (from PIF)

; ----------------------------------------------------------
; Video data sampling
; ----------------------------------------------------------

public n64_video_start:
.wrap_target

    ; Wait for high CLK
    wait 1 pin 8

    ; Sample 4 bytes in total
    set x, 2

    ; Wait for low CLK
    wait 0 pin 8

    ; if DSYNC: goto n64_video_start
    jmp pin n64_video_start

    ; DSYNC == 0

    ; Sample first byte if data + dsyncn
    in pins, 8

n64_video_capture_loop:

    ; Wait for high CLK
    wait 1 pin 8
    ; Wait for low CLK
    wait 0 pin 8

    ; Sample data + dsyncn
    in pins, 8

    jmp x-- n64_video_capture_loop

    ; Auto-push is enabled, saves one instruction/cycle
    ; push

    ; Utilize wrap insead
    ; jmp n64_video_start
.wrap

; ----------------------------------------------------------
; Audio data sampling
; ----------------------------------------------------------

public n64_audio_start:

    ; Loop {
    ;   Wait for rising edge of LRCLK (known initial state, recover from lost sync)
    ;   Do {
    ;     Iteration 1:
    ;       LRCLK == 1 (Left channel)
    ;       BCLK  == 1 or 0
    ;       Skip the first bit (stray bit)
    ;       Wait for BCLK == 0
    ;       Set X = 15
    ;       Wait for BCLK == 1
    ;       Do {
    ;         Wait for rising edge of BCLK
    ;         Sample SDAT into ISR
    ;       } While X--
    ;       // ISR has 16 bits of sample data now
    ;     Iteration 2:
    ;       LRCLK == 1 (Left channel)
    ;       BCLK  == 1
    ;       Skip the first bit (stray bit)
    ;       Wait for BCLK == 0
    ;       LRCLK == 0 (Right channel now)
    ;       Set X = 15
    ;       Wait for BCLK == 1
    ;       Do {
    ;         Wait for rising edge of BCLK
    ;         Sample SDAT into ISR
    ;       } While X--
    ;       // ISR has 32 bits of sample data now
    ;   } While LRCLK == 1 // loop only once in practice - If not, endless loop, no push
    ;   Push // By doing this manually, the ISR should be 2x16 bit words in order)
    ; }

    ; The following ensures that LRCLK is in sync.
    ; This is okay to do when we loop as well,
    ; since we just finished capturing the right channel.
    ; Ensure that LRCLK = 0 (Right channel active)
    wait 0 gpio 9

    ; Ensure that LRCLK = 1 (Left channel active)
    wait 1 gpio 9

    ; This label is used when capturing the second word
n64_audio_capture_word:

    ; ------
    ; Skip the first sample (17th stray bit of previous sample)
    ; Wait for low BCLK
    wait 0 gpio 11

    ; Time between this step and the next is a few microseconds.
    ; Could use time to set variables etc.
    ; Set x=15 (Sample 16 bits)
    set x, 15

    ; Wait for high BCLK
    wait 1 gpio 11

    ; End of skip
    ; ------

n64_audio_left_capture_loop:
    ; Wait for low BCLK
    wait 0 gpio 11

    ; Wait for high BCLK
    wait 1 gpio 11

    ; Sample a data bit
    in pins, 1

    jmp x-- n64_audio_left_capture_loop

; ----------------------------------------------------------
; Left channel has been captured. Capture right channel now.
; ----------------------------------------------------------

    ; Iterate once more to capture the right channel.
    ; To do this in one instruction without using x/y, let's use the LRCLK pin.
    ; An added bonus is that we end up in an endless loop here without pushing.
    ; This means that we will _not_ push junk data to the audio buffer, i.e.
    ; there should not be any harsh noise in case of a random un-synced state.
    jmp pin n64_audio_capture_word

; ----------------------------------------------------------
; Right channel has been captured.
; ----------------------------------------------------------

public n64_audio_wrap:
    ; Auto-push is disabled in order to have better control over the ISR bit-alignment.
    push

    ; Loop forever utilizing a second wrap (can't use .wrap/.wrap_target a second time)
    ; jmp n64_audio_start


; ----------------------------------------------------------
; Joybus RX data sampling
; Based on: https://github.com/Polprzewodnikowy/PicoJoybus/blob/main/src/joybus.pio
; Original author: Polprzewodnikowy / Mateusz Faderewski
; ----------------------------------------------------------

public n64_joybus_rx_start:

rx_stop:
rx_good_data:
    push iffull noblock
public rx_start:
    wait 1 pin 0
    wait 0 pin 0
    mov isr, null
    set x, 7 [13]
rx_bit_loop:
    in pins, 1
    jmp !x rx_data [8]
    nop [8]
    jmp pin rx_stop
    jmp x-- rx_bit_loop [12]
rx_data:
    jmp pin rx_good_data
rx_bad_data:
    jmp rx_start





% c-sdk {

#include <hardware/clocks.h>

void n64_video_program_init(PIO pio, uint sm, uint offset)
{
    // gpio0 -> 8 input
    for (int i = 0; i <= 8; i++) {
        pio_gpio_init(pio, i);
    }

    pio_sm_config c = n64_program_get_default_config(offset);

    // Double the FIFO depth
    sm_config_set_fifo_join(&c, PIO_FIFO_JOIN_RX);

    // Enable auto-push
    sm_config_set_in_shift(&c, true, true, 32);

    // GPIO0 -> GPIO8 as in pins
    sm_config_set_in_pins(&c, 0);

    // JMP pin = DSYNCn
    sm_config_set_jmp_pin(&c, 7);

    pio_sm_init(pio, sm, offset, &c);
}

void n64_audio_program_init(PIO pio, uint sm, uint offset)
{
    // gpio9 -> 11 input
    for (int i = 9; i <= 11; i++) {
        pio_gpio_init(pio, i);
    }

    // Configure wrap/wrap_target for the audio program
    pio_sm_config c = pio_get_default_sm_config();
    sm_config_set_wrap(&c, offset + n64_offset_n64_audio_start, offset + n64_offset_n64_audio_wrap);

    // Double the FIFO depth
    sm_config_set_fifo_join(&c, PIO_FIFO_JOIN_RX);

    // MSB first / shift left, disable autopush
    sm_config_set_in_shift(&c, false, false, 32);

    // SDAT/GPIO10 as in start pin
    sm_config_set_in_pins(&c, 10);

    // JMP pin = LRCLK
    sm_config_set_jmp_pin(&c, 9);

    pio_sm_init(pio, sm, offset + n64_offset_n64_audio_start, &c);
}

void n64_joybus_rx_program_init(PIO pio, uint sm, uint offset, uint pin)
{
    // pio_sm_config c = pio_get_default_sm_config();
    // sm_config_set_wrap(&c, offset + n64_offset_n64_audio_start, offset + n64_offset_n64_audio_wrap);
    pio_sm_config c = n64_program_get_default_config(offset);

    // Double the FIFO depth
    sm_config_set_fifo_join(&c, PIO_FIFO_JOIN_RX);

    sm_config_set_in_pins(&c, pin);
    sm_config_set_jmp_pin(&c, pin);

    sm_config_set_in_shift(&c, false, false, 8);

    float div = ((float) (clock_get_hz(clk_sys))) / (32 * 250000);
    sm_config_set_clkdiv(&c, div);

    gpio_pull_up(pin);
    pio_gpio_init(pio, pin);

    pio_sm_init(pio, sm, offset + n64_offset_n64_joybus_rx_start, &c);
    pio_sm_set_enabled(pio, sm, true);
}
%}
